<programa> ::= "inicio" <cuerpo> "fin"

<cuerpo> ::= { <declaracion>
             | <instruccion>
             | <declaracion_clase>
             | <tratamiento_error>
             | <seleccion_multiple>
             | <para>
             | <do_mientras> }

<declaracion> ::= "var" <identificador> ":" <tipo> ";"

<tipo> ::= "ChicoEntero"      (* corresponde a int *)
         | "Flotante"         (* corresponde a float *)
         | "doble"            (* corresponde a double *)
         | "VerdaderoOFalso"  (* corresponde a boolean *)
         | "string"
         | "Letra"            (* corresponde a char *)

<instruccion> ::= <asignacion>
                | <decision>
                | <ciclo>
                | <llamada_funcion>
                | <bloque>

<asignacion> ::= <identificador> "=" <expresion> ";"

<decision> ::= "PreguntaExistencial" "(" <expresion> ")" <bloque> [ "PuesSiNo" <bloque> ]

<ciclo> ::= "Mientras" "(" <expresion> ")" <bloque>

<para> ::= "Cuatro" "(" <asignacion> <expresion> ";" <asignacion> ")" <bloque>

<do_mientras> ::= "DoMientras" <bloque> "while" "(" <expresion> ")" ";"

<llamada_funcion> ::= <identificador> "(" [ <lista_expresiones> ] ")" ";"
<lista_expresiones> ::= <expresion> { "," <expresion> }

<bloque> ::= "{" <instrucciones> "}"
<instrucciones> ::= { <instruccion> }

<tratamiento_error> ::= "try" <bloque> "AgarraElError" "(" <identificador> ")" <bloque>

<seleccion_multiple> ::= "switch" "(" <expresion> ")" "{" { <caso> } [ <defecto> ] "}"
<caso> ::= "Opcion" <valor> ":" <instrucciones>
<defecto> ::= "default" ":" <instrucciones>

<declaracion_clase> ::= "Clase" <identificador> "{" <cuerpo_clase> "}"
<cuerpo_clase> ::= { <declaracion> | <instruccion> }

<expresion> ::= <expresion_logica>
<expresion_logica> ::= <expresion_relacional> { <operador_logico> <expresion_relacional> }
<expresion_relacional> ::= <expresion_aritmetica> [ <operador_relacional> <expresion_aritmetica> ]
<expresion_aritmetica> ::= <termino> { <operador_aritmetico> <termino> }
<termino> ::= <factor> { ( "*" | "/" ) <factor> }
<factor> ::= "(" <expresion> ")" | <valor> | <identificador>

<valor> ::= <numero> | <bool> | <cadena>

<operador_aritmetico> ::= "+" | "-" | "*" | "/"
<operador_relacional> ::= "==" | "!=" | "<" | "<=" | ">" | ">="
<operador_logico> ::= "&&" | "||"

<numero> ::= <digito> { <digito> }
<digito> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

<bool> ::= "true" | "false"

<cadena> ::= '"' { <caracter> } '"'
<caracter> ::= cualquier car√°cter excepto '"'

<identificador> ::= <letra> { <letra> | <digito> }
<letra> ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j"
          | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t"
          | "u" | "v" | "w" | "x" | "y" | "z"
          | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J"
          | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T"
          | "U" | "V" | "W" | "X" | "Y" | "Z"
